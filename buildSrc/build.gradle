/*
   Copyright (C) 2015 Bj√∂rn Quentin

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

     http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
 */

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'java-gradle-plugin'

def pluginVersion = '0.7.8'

buildscript {
    repositories {
        jcenter()
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

compileGroovy {
    //sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

tasks.withType(JavaCompile) {
    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            group 'com.github.bjoernq'
            artifactId 'unmockplugin'
            version pluginVersion

            from components.java

            artifact sourceJar {
                classifier "sources"
            }

            artifact javadocJar

            pom {
                name = 'UnMock Plugin'
                description = 'Gradle plugin to be used in combination with the new unit testing feature of the Gradle Plugin / Android Studio to use real classes for e.g. SparseArray.'
                url = 'https://github.com/bjoernq/unmock-plugin'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'bjoernQ'
                        name = 'Bjoern Quentin'
                        email = 'bjoern.quentin@mobile-j.de'
                    }
                }
                scm {
                    connection = 'https://github.com/bjoernq/unmock-plugin'
                    developerConnection = 'https://github.com/bjoernq/unmock-plugin'
                    url = 'https://github.com/bjoernq/unmock-plugin'
                }
            }
        }
    }

    repositories {
        maven {
            name = "central"
            credentials {
                username "$centralUsername"
                password "$centralPassword"
            }
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile 'org.javassist:javassist:3.27.0-GA'
    compile 'com.android.tools.build:gradle:3.3.0'

    testCompile 'junit:junit:4.13'
    testCompile gradleTestKit()
}
